//========================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// -----------------------------------------
// - 结婚 脚本 -
//========================
//= 从Japanda转来
//========================
prt_church.gat,94,100,4	script	结婚 服务人员	71,{
	cutin "wedding_marry01",2;
	mes "[结婚 快乐]";
	mes "所谓结婚是指，";
	mes "期待着幸福，所有人们的希望，";
	mes "请问你是否有，";
	mes "想要结婚的对象吗?";
	next;
	switch (select("请说明有关结婚典礼的事情","结婚典礼的过程是什么样子呢?","想要申请结婚典礼","我是无敌的单身军团!")) {
	case 1:
		mes "[结婚 快乐]";
		mes "目前是由教主";
		mes "负责结婚，由于忙碌";
		mes "来主管结婚典礼";
		next;
		mes "[结婚 快乐]";
		mes "因为教主先生，把很多单身贵族，";
		mes "结合成很多情侣的关系，";
		mes "所以也有 '爱的教主 '昵称，";
		mes "呵呵...";
		next;
		mes "[结婚 快乐]";
		mes "虽然每天忙碌于国家大事，";
		mes "为了即将新出发的小俩口，";
		mes "特地来到了普隆德拉教堂，";
		mes "期待所有人都能过着幸福的生活，";
		mes "这是教主给的恩德";
		next;
		mes "[结婚 快乐]";
		mes "求婚要慎重，";
		mes "郑重的提出，";
		mes "只要有结过一次婚的人，";
		mes "再也不能恢复原状，结婚的两个人，";
		mes "除非离开这人世间为止，";
		mes "否则就永远会在一起";
		next;
		mes "[结婚 快乐]";
		mes "另外男性对女性，";
		mes "女性只能对男性结婚，";
		mes "另外也有可能，";
		mes "有些人会提出，和同性或者和魔物结婚，";
		mes "这样的主张... 是不会被成立的";
		next;
		mes "[结婚 快乐]";
		mes "假如有约定好未来的对象的话，";
		mes "不妨试着求婚看看吧，";
		mes "愿期待未来幸福的所有恋人，";
		mes "祝福美满!!";
		close2;
		cutin "",255;
		end;
	case 2:
		mes "[结婚 快乐]";
		mes "首先先确认新郎新娘们，";
		mes "都有向我申请吗，";
		mes "当然两位都是，没有结过婚的人才对啊?";
		mes "假如有申请的话，只请两位组对之后，";
		mes "请到教主那里吧";
		next;
		mes "[结婚 快乐]";
		mes "到教主那里后，";
		mes "^0000FF男方^000000先说话，";
		mes "然后再说要结婚女性的名字，";
		mes "名字不能讲错，要讲正确，";
		mes "结婚典礼才能执行";
		next;
		mes "[结婚 快乐]";
		mes "当讲完话时，";
		mes "紧接着女方跟教主讲话，";
		mes "教主会问你，";
		mes "你要结婚对象的名字，";
		mes "相对的也一样的回答男性的名字";
		next;
		mes "[结婚 快乐]";
		mes "假如结婚对象的名字正确的话，";
		mes "会交换结婚戒指，";
		mes "从那瞬间开始，";
		mes "两个人会永远的在一起，不错吧?";
		mes "说不定被委婉的拒绝也不一定，";
		mes "也不要太难过啊";
		next;
		mes "[结婚 快乐]";
		mes "当想要结婚的人，";
		mes "太多的聚集在一起时，";
		mes "请大家遵守秩序，按顺序的来跟国王说话，";
		mes "教主不可能同时的，";
		mes "主持好几对婚礼";
		next;
		mes "[结婚 快乐]";
		mes "最后一点，讲完名字后，";
		mes "回答要快";
		mes "^ff0000当新郎讲完话，在 3分钟之内，";
		mes "把所有的过程都做完^000000吧 要不然，";
		mes "结婚典礼会停止，";
		mes "再也不能结婚了";
		next;
		mes "[结婚 快乐]";
		mes "写结婚对象的名字，最简单的方法是，";
		mes "先寄给对方一张纸条，";
		mes "在对话视窗里放名字的部份，";
		mes "点选箭头标示，";
		mes "左边即可";
		next;
		mes "[结婚 快乐]";
		mes "那就会显示送纸条给对方的名字，";
		mes "按住 ctrl键的同时，";
		mes "再按 C键，那名字会被记录，";
		mes "以后要放入名字时，";
		mes "按住 shift键的同时，";
		mes "再按 insert 键，那么名字就很快的，";
		mes "很方便的写在上面了";
		next;
		mes "[结婚 快乐]";
		mes "那么就试验一次吧，";
		mes "把想要结婚对象的名字，";
		mes "依告诉你的方式写一遍，";
		mes "按住 ctrl键的同时，";
		mes "再按 C键是记录... ?";
		mes "按住 shift键的同时，";
		mes "再按 insert 键";
		next;
		input @test$;
		mes "[结婚 快乐]";
		mes "如何?会了吗?";
		next;
		mes "[结婚 快乐]";
		mes "很好，写的不错，";
		mes "若在你的心理已决定好了的话，一定要来";
		mes "这里申请";
		close2;
		cutin "",255;
		end;
	case 3:
		cutin "wedding_marry02",2;
		mes "[结婚 快乐]";
		mes "想要结婚的话要申请噢，";
		mes "男性要交出^0000881,300,000金币和";
		mes "燕尾服1件^000000，女性要交^0000881,200,000";
		mes "金币和结婚礼服1件^000000";
		mes "然后填写申请书即可，";
		mes "假如男女双方都有申请的话，";
		mes "就可以结婚了";
		next;
		mes "[结婚 快乐]";
		mes "别忘了还要准备结婚戒指，";
		mes "男女双方都需要^000088钻石戒指1个^000000";
		mes "当申请的时候，一起给我就好了";
		next;
		mes "[结婚 快乐]";
		mes "两位都申请好之后，";
		mes "就可以马上执行结婚典礼了，";
		mes "请问你现在要申请结婚吗?";
		next;
		if(select("是","否")==2) {
			mes "[结婚 快乐]";
			mes "祝你有一个幸福的一天..!";
			close2;
			cutin "",255;
			end;
		}
		if(PartnerId) {
			mes "[结婚 快乐]";
			mes "已申请过就不能再申请";
			close2;
			cutin "",255;
			end;
		}
		if(Upper&2) {
			//养子未调查
			close2;
			cutin "",255;
			end;
		}
		if(@event_marry) {
			mes "[结婚 快乐]";
			mes "嗯...? 之前你有申请过啊，";
			mes "因为已经都申请好了，";
			mes "再问一下要结婚的对象，";
			mes "是否有申请好了呢，";
			mes "假如两位都有申请好的话，";
			mes "请到教主那里，举行结婚典礼吧";
			close2;
			cutin "",255;
			end;
		}
		if(BaseLevel<45) {
			mes "[结婚 快乐]";
			mes "从求婚来说，为了自己能";
			mes "在结婚的义务或保护对方性命，";
			mes "请基本等级达45以上";
			close2;
			cutin "",255;
			end;
		}
		if(countitem(2613)<1) {
			mes "[结婚 快乐]";
			mes "结婚戒指呢?";
			mes "好像忘记带了喔!";
			mes "仔细的找找之后，再来找我吧!";
			close2;
			cutin "",255;
			end;
		}
		if((Sex==0 && countitem(2338)<1) || (Sex==1 && countitem(7170)<1)) {
			mes "[结婚 快乐]";
			if(Sex==0) {
				mes "哎呀!结婚礼服呢?";
				mes "这可是婚礼上新娘的装扮..";
			}
			else {
				mes "哎呀!燕尾服呢?";
				mes "这可是婚礼上新郎的装扮..";
			}
			mes "是一生一次的事情，请别忘记准备!";
			close2;
			cutin "",255;
			end;
		}
		if((Sex==0 && Zeny<1200000) || (Sex==1 && Zeny<1300000)) {
			mes "[结婚 快乐]";
			mes "很抱歉!婚礼的费用需要";
			if(Sex==0)
				mes "1,200,000zeny。";
			else
				mes "1,300,000zeny。";
			close;
		}
		mes "[结婚 快乐]";
		mes "先在这里恭喜你结婚啊!";
		mes "不知道对象是谁，应该很幸福啊";
		next;
		mes "[结婚 快乐]";
		mes "那就开始接受申请书啦，";
		mes "在这里写一下自己的名字吧";
		while(1) {
			next;
			input .@name$;
			if(.@name$ != strcharinfo(0)) {
				mes "[结婚 快乐]";
				mes "唉唷! 连名字都不会写正确啊，";
				mes "还想要结婚?";
				mes "先去学一下，写名字的方法后再来吧，";
				mes "写快一点会比较好啊，";
				mes "请你再写一次";
				continue;
			}
			break;
		}
		delitem 2613,1;
		if(Sex==0) {
			delitem 2338,1;
			set Zeny,Zeny-1200000;
		}
		else {
			delitem 7170,1;
			set Zeny,Zeny-1300000;
		}
		set @event_marry,1;
		mes "[结婚 快乐]";
		mes "是的，正确的接收到结婚申请书了，";
		mes "以后就是已这种方式，";
		mes "对教主说出要结婚对象的名字，";
		mes "要结婚人的名字也要正确知道啊，";
		mes "了解吗?";
		mes "提早做好准备之后再去吧";
		next;
		mes "[结婚 快乐]";
		mes "那么"+strcharinfo(0)+" 先生 / 小姐...";
		mes "再问一下要结婚的对象，";
		mes "是否有申请好了呢，";
		mes "假如两位都有申请好的话";
		mes "赶快到教主那里，举行结婚典礼吧";
		mes "最后...";
		mes "要幸福啊，愿天下有情人终成眷属~";
		emotion 3;
		close2;
		cutin "",255;
		end;
	case 4:
		cutin "wedding_marry02",2;
		emotion 23;
		hideoffnpc "单身军团#普隆德拉";
		hideoffnpc "单身军团#吉芬";
		hideoffnpc "单身军团#梦罗克";
		hideoffnpc "单身军团#斐扬";
		hideoffnpc "单身军团#天津町";
		hideoffnpc "单身军团#克魔岛";
		emotion 27,"身军军团#普隆德拉";
		emotion 27,"单身军团#吉芬";
		emotion 27,"单身军团#梦罗克";
		emotion 27,"单身军团#斐扬";
		emotion 27,"单身军团#天津町";
		emotion 27,"单身军团#克魔岛";
		mes "[单身军团]";
		mes "精练要自己做，才会做出好东西!";
		mes "在迷宫里面组队是奢侈的行为!";
		mes "我是有决心的 NPC!";
		mes "从出生到转职，都是自己练的!";
		mes "过圣诞节的时候，到外面拔草求生!";
		mes "我们是无敌的单身军团!";
		next;
		hideonnpc "单身军团#普隆德拉";
		hideonnpc "单身军团#吉芬";
		hideonnpc "单身军团#梦罗克";
		hideonnpc "单身军团#斐扬";
		hideonnpc "单身军团#天津町";
		hideonnpc "单身军团#克魔岛";
		emotion 4;
		close2;
		cutin "",255;
		end;
	}
OnInit:
	hideonnpc "单身军团#普隆德拉";
	hideonnpc "单身军团#吉芬";
	hideonnpc "单身军团#梦罗克";
	hideonnpc "单身军团#斐扬";
	hideonnpc "单身军团#天津町";
	hideonnpc "单身军团#克魔岛";
	end;
}

//========================================================================================
prt_church.gat,100,128,4	script	教主	60,{
	cutin "wedding_bomars01",2;
	if(.flag==1 || (.flag==2 && strcharinfo(0) != .bride$)) {
		mes "[宝玛乐丝]";
		mes "现在有人的婚礼正在进行呢...";
		mes "请你等等吧";
		close2;
		cutin "wedding_bomars01",255;
		end;
	}
	if(PartnerId) {
		mes "[宝玛乐丝]";
		mes "您已经结过婚，祝您幸福";
		close2;
		cutin "wedding_bomars01",255;
		end;
	}
	if(@event_marry==0) {
		mes "[宝玛乐丝]";
		mes "想要结婚的话，先申请吧";
		close2;
		cutin "wedding_bomars01",255;
		end;
	}
	if(Sex==1) {
		getpartymember getcharid(1);
		for(set .@i,0; .@i<2; set .@i,.@i+1) {
			if($@partymembername$[.@i] != strcharinfo(0))
				set .@another$,$@partymembername$[.@i];
		}
		if($@partymembercount != 2 || readparam(21,.@another$) == 1) {
			mes "[宝玛乐丝]";
			mes "想要结婚的话，";
			mes "请限定两位先组队之后，再来吧";
			close2;
			cutin "wedding_bomars01",255;
			end;
		}
		if(readparam(56,.@another$)&2) {
			close2;
			cutin "wedding_bomars01",255;
			end;
		}
		mes "[宝玛乐丝]";
		mes "要结婚吗...?";
		mes "是"+strcharinfo(0)+"...啊?";
		mes "你要申请结婚典礼吗?";
		next;
		if(select("是的","不是")==2) {
			mes "[宝玛乐丝]";
			mes "好好想想吧，毕竟这是你的选择...";
			close2;
			cutin "wedding_bomars01",255;
			end;
		}
		set .flag,1;
		initnpctimer;
		set .groom$,strcharinfo(0);
		announce "宝玛乐丝: 是" + .groom$ + "君的求婚啊...",9;
		mes "[宝玛乐丝]";
		mes "是这样啊，好的..";
		mes "告诉我那新娘的名字吧?";
		next;
		input .bride$;
		if(.bride$ != .@another$) {
			announce "宝玛乐丝: 好像准备不足...先进行下一个婚礼。",9;
			mes "[宝玛乐丝]";
			mes "哦!?我的上帝啊!";
			mes "你居然记错了另一半的名字!";
			mes "看来双方的意见还没有统一,好好商量一下吧!!";
			stopnpctimer;
			set .flag,0;
			close2;
			cutin "wedding_bomars01",255;
			end;
		}
		if(attachrid(getcharid(3,.bride$))) {
			set @defend,0;
			detachrid;
		}
		if(attachrid(getcharid(3,.groom$))) {
			announce "宝玛乐丝:"+.groom$+" 君对 "+.bride$+" 小姐提出结婚的要求了",9;
			mes "[宝玛乐丝]";
			mes "总而言之..你会照顾她一辈子吧?";
			mes "来点发誓性质的词语吧XD...";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "我愿意承受接纳 "+.bride$+" 做我的妻子，诚实遵照上帝的旨命，和他生活在一起";
			next;
			announce "宝玛乐丝:现在应该换问女方了，让我来听听 "+.bride$+" 小姐的意思如何吧?",9;
			mes "[宝玛乐丝]";
			mes "现在应该换问女方了，我要听听她的意见";
			set .flag,2;
			set @event_marry,0;
		}
		close2;
		cutin "wedding_bomars01",255;
		end;
	}
	if(.flag==0) {
		mes "[宝玛乐丝]";
		mes "要结婚的话...请男方过来申请吧";
		close2;
		cutin "wedding_bomars01",255;
		end;
	}
	mes "[宝玛乐丝]";
	mes "相逢即是有缘";
	next;
	announce "宝玛乐丝: 新娘" +.bride$+ "，请问你...",9;
	mes "[宝玛乐丝]";
	mes "" + .bride$+ "";
	mes "你要跟他常相携手、生死与共";
	mes "的新郎的名子是";
	mes "" +.groom$+ "";
	next;
	if(select("是的","不是")==2) {
		mes "[宝玛乐丝]";
		mes "怎么了";
		announce "宝玛乐丝: 好像准备不足...先进行下一个婚礼。",9;
		stopnpctimer;
		set .flag,0;
		close2;
		cutin "wedding_bomars01",255;
		end;
	}
	mes "[宝玛乐丝]";
	mes "新娘" +.bride$+ "";
	mes "环境无论是好是坏，";
	mes "是富贵是贫贱，";
	mes "是健康是疾病，";
	mes "是成功是失败，";
	mes "永远照顾着"+.groom$+ "";
	next;
	if(select("我愿意","...我不要")==2) {
		mes "[宝玛乐丝]";
		mes "怎么了";
		announce "宝玛乐丝: 好像准备不足...先进行下一个婚礼。",9;
		stopnpctimer;
		set .flag,0;
		close2;
		cutin "wedding_bomars01",255;
		end;
	}
	if(@defend) {
		set @event_marry,0;
		mes "时间到...";
		close2;
		cutin "wedding_bomars01",255;
		end;
	}
	//结婚处理
	if(getcharid(3,.groom$)<=0) {
		stopnpctimer;
		set .flag,0;
		set @event_marry,0;
		mes "新郎不在";
		close2;
		cutin "wedding_bomars01",255;
		end;
	}
	set .@dummy,marriage(.groom$);
	wedding;
	sc_start SC_Wedding,3600000,1;
	getitem 2635,1;
	attachrid(getcharid(3,.groom$));
	sc_start SC_Wedding,3600000,1;
	getitem 2634,1;
	detachrid;
	attachrid(getcharid(3,.bride$));
	set .flag,0;
	set @event_marry,0;
	stopnpctimer;
	announce "宝玛乐丝:那么我在此宣布，祝贺你们成为夫妻!",9;
	announce "宝玛乐丝:"+.groom$+" 和 "+.bride$+" 的婚礼完成了!",9;
	mes "[宝玛乐丝]";
	mes "祝你们幸福..";
	close2;
	cutin "wedding_bomars01",255;
	end;
//新郎说完话之后，新娘如果在三分钟之内没有说话...
OnTimer180000:
	announce "宝玛乐丝: 回答太慢了...下一位",9;
	set $@marry,0;
	end;
}

//=======================Solo军团==============================
prt_church.gat,97,102,0	script	单身军团#普隆德拉	105,{
	mes "[单身军团]";
	mes "精练要自己做，才会做出好东西!";
	close;
}

prt_church.gat,98,102,0	script	单身军团#吉芬	705,{
	mes "[单身军团]";
	mes "在洞穴里面组队是奢侈的行为!";
	close;
}

prt_church.gat,99,102,0	script	单身军团#梦罗克	707,{
	mes "[单身军团]";
	mes "我可以施放出心型的 NPC!";
	close;
}

prt_church.gat,100,102,0	script	单身军团#斐扬	708,{
	mes "[单身军团]";
	mes "从出生到转职，都是自己练的!";
	close;
}

prt_church.gat,101,102,0	script	单身军团#天津町	767,{
	mes "[单身军团]";
	mes "过圣诞节的时候，到外面拔草求生!";
	close;
}

prt_church.gat,102,102,0	script	单身军团#克魔岛	780,{
	mes "[单身军团]";
	mes "...我们是无敌的单身军团!";
	close;
}
