
//============================================================
// 炼金 - 属性药水制造
//============================================================
yuno_in04.gat,33,108,5	script	皮斯瑞克	750,{
	mes "[皮斯瑞克]";
	if(BaseJob!=Job_Alchem) goto L_noalche;
	if(countitem(7434)) goto L_alreadyhave;
	if(JobLevel<40) goto L_nojob;

	mes "要我教你新技能";
	mes "但你得带些东西给我";
	mes "5 黄色魔力矿石";
	mes "4 空药瓶";
	mes "10 人鱼之心";
	mes "10 蛾蝶虫翅膀粉末";
	mes "20 噬人树花";
	mes "1 旋转眼镜0洞";
	next;
	
	mes "[皮斯瑞克]";
	mes "让我看看你带来的物品..";
	next;
	
	if(countitem(715)<5 || countitem(1093)<4 || countitem(950)<10 || countitem(1057)<10 || countitem(1032)<20 || countitem(2243)<1) goto L_noitems;//Items: Yellow_Gemstone, Empty_Potion_Bottle, Heart_of_Mermaid, Moth_Dust, Maneater_Blossom, Geek_Glasses,
	delitem 715, 5;//Items: Yellow_Gemstone,
	delitem 1093, 4;//Items: Empty_Potion_Bottle,
	delitem 950, 10;//Items: Heart_of_Mermaid,
	delitem 1057, 10;//Items: Moth_Dust,
	delitem 1032, 20;//Items: Maneater_Blossom,
	delitem 2243, 1;//Items: Geek_Glasses,
	getitem 7434, 1;//Items: Elemental_Potion_Creation_Guide,

	mes "[皮斯瑞克]";
	mes "我想你都带来了,";
	mes "我把我的手册也给你.";
	close;
	
L_noitems:
	mes "[皮斯瑞克]";
	mes "你没足够的东西.";
	mes "等你有了再来找我";
	close;
	
L_noalche:
	mes "对不起，我的知识仅限于炼金.";
	mes "很遗憾不能帮你..";
	emotion e_gasp;
	close;

L_alreadyhave:
	mes "我已经给你啦";
	mes "我的属性药水制造手册...";
	close;

L_nojob:
	mes "当你成为一名炼金时再来我找吧";
	close;
}


//============================================================
// 炼金 - 生命伦理(人工生命体任务)
//============================================================

//=====================Requires Bug Testing=====================
lhz_in01.gat,46,125,6	script	雷根贝勒 卫兵#99	867,{
	mes "[雷根贝勒 卫兵]";
	if(isequipped(2241) && isequipped(2243)){
		mes "......................";
		next;
		if(select("今天天气不错,是吧?:取消")==1){
			mes "[雷根贝勒 卫兵]";
			mes ".....";
			close2;
			warp "lhz_in01.gat",282,172;
			end;
		}
	}
	mes "...";
	close;
}

lhz_in01.gat,204,138,6	script	凯西贝安	750,{
	if(bioeth >= 2){
		mes "[凯西贝安]";
		mes "哈~";
		mes "你是来学习跟人工生命体相关的知识吗?";
		next;
		mes "[凯西贝安]";
		mes "可是我实在不应该告诉你!";
		mes "你可以去找里希塔乐镇的布陵澈";
		mes "应该会有些帮助!";
		if(bioeth == 2) set bioeth,3;
		close;
	}
	mes "[凯西贝安]";
	mes "哈~";
	mes "你一定是从炼金术士研究所来的,对吧?!";
	mes "很高兴见到你!";
	next;
	mes "[凯西贝安]";
	mes "我听说那里的炼金术士都很厉害!";
	mes "我真的很希望有机会可以跟他们合作看看...";
	close;
}

lhz_in01.gat,224,140,8	script	柯拉休斯	57,{
	if(MISC_QUEST&64){
		mes "[柯拉休斯]";
		mes "我已经把技能教给你了...";
		if(getskilllv(238) == 0 && BaseJob == Job_Alchem) skill 238,1,0;
		close;
	}
	switch(bioeth){
	case 4:
		mes "[柯拉休斯]";
		mes "...你真的很不受教!";
		mes "我不觉得我可以教你跟人工生命体相关的技能,";
		mes "你根本一点都不懂这方面的知识!";
		next;
		menu "...可是我已经学过了",-;
		mes "[柯拉休斯]";
		mes "喔? 真的吗?";
		mes "那我要先做几个测试,";
		mes "看看你到底懂多少?";
		next;
		mes "[柯拉休斯]";
		mes "第一个问题，";
		mes "什么技能可以将生命体移动到眼睛看不见的地方做休息?";
		input @kelques$;
		next;
		mes "[柯拉休斯]";
		if(@kelques$ != "安息"){
			mes "哼! 我就知道你根本什么都不懂!";
			close;
		}
		mes "答对了! 第二个问题.";
		mes "炼金术士可以利用什么东西招唤人工生命体?";
		input @kelques$;
		next;
		mes "[柯拉休斯]";
		if(@kelques$ != "艾普理欧"){
			mes "哼! 我就知道你根本什么都不懂!";
			close;
		}
		mes "还不错嘛~不过还有最后一个问题!";
		mes "什么技能可以将战斗中死亡的生命体重新招唤出来?";
		input @kelques$;
		next;
		mes "[柯拉休斯]";
		if(@kelques$ != "复活生命体"){
			mes "哼! 我就知道你根本什么都不懂!";
			close;
		}
		mes "经过这些讨论之后,";
		mes "我决定我以后不会再教任何人有关人工生命体的技能了!";
		mes "这是我最后的决定!";
		set bioeth,5;
		close;
	case 5:
		mes "[柯拉休斯]";
		mes "经过这些讨论之后,";
		mes "我决定我以后不会再教任何人有关人工生命体的技能了!";
		mes "这是我最后的决定!";
		close;
	case 6:
	//Unofficial text and story -.-;
		mes "[柯拉休斯]";
		mes "喔~ 又是你? 看来你已经决定了!";
		mes "嗯~ 大部分的炼金术士都是不会放弃自己的梦想的!";
		next;
		menu "让我们谈点别的吧...",-;
		mes "[柯拉休斯]";
		mes "你见过我儿子? 他说了什么?";
		next;
		mes "[柯拉休斯]";
		mes ".....";
		next;
		mes "[柯拉休斯]";
		mes "他希望我回家吗? 嗯...";
		mes "我想我该给你一些回报,";
		mes "拿去吧! 这是你想知道的有关人工生命体的秘密!";
		if(BaseJob == Job_Alchem) skill 238,1,0;
		set MISC_QUEST,MISC_QUEST|64;
		set bioeth,0;
		close;
	default:
		break;
	}
	switch(@keltalk){
	case 1:
		mes "[柯拉休斯]";
		mes "谁决定什么是对是错?";
		mes "是什么注定会被了解而哪些秘密将永远无法预料会被人类所理解?";
		set @keltalk,2;
		close;
	case 2:
		mes "[柯拉休斯]";
		mes "难道我没有跟你说不要再来了吗?";
		mes "请你马上离开!";
		mes "难道你没有其他事情做了吗?";
		mes "我不会告诉你任何关于人工生命体的知识的!";
		set @keltalk,3;
		set bioeth,1;
		close;
	case 3:
		mes "[柯拉休斯]";
		mes "难道我没有跟你说不要再来了吗?";
		mes "请你马上离开!";
		mes "难道你没有其他事情做了吗?";
		mes "我不会告诉你任何关于人工生命体的知识的!";
		close;
	default:
		mes "[柯拉休斯]";
		mes "谁决定什么是对是错?";
		mes "是什么注定会被了解而哪些秘密将永远无法预料会被人类所理解?";
		if(BaseJob == Job_Alchem) set @keltalk,1;
		close;
	}
}

lhz_in01.gat,225,122,4	script	苏格拉撒德	754,{
	mes "[苏格拉撒德]";
	mes "炼金术是很令人惊奇的...";
	mes "它融合了许多科学以及各种不同的知识,";
	mes "但这并不是随便什么人都可以学会怎么使用的...";
	next;
	mes "[苏格拉撒德]";
	mes "想你想想看,虽然说是由人类所创造,";
	mes "但是生命体也是一种生命,";
	mes "将生命任意的安息又招唤出来...虽然是可行的,";
	mes "不过还不鼓励这种不太好的行为...";
	if(bioeth == 1) set bioeth,2;
	close;
}
lhz_in02.gat,269,273,4	script	布陵澈	709,{

	if(bioeth == 3){
		mes "[布陵澈]";
		mes "喔? 你是来打听有关人工生命体的事情的吗?";
		mes "嗯... 很抱歉... 我也帮不上什么忙...";
		next;
		mes "[布陵澈]";
		mes "不过,我听过有种技能叫做";
		mes "复活生命体";
		mes "听起来在使用生命体上,这是个很重要的技能!";
		set bioeth,4;
		close;
	}
	mes "[布陵澈]";
	mes "这真是个很棒的旅馆,对吧?";
	mes "在这里我可以很放松,";
	mes "不用管那些烦人的事...";
	close;
}
lhz_in03.gat,106,34,4	script	寇凌	706,{

	if(bioeth == 5){
		mes "[寇凌]";
		mes "你在研究所见过我爸爸?";
		mes "帮我转告他我很想他!";
		set bioeth,6;
		close;
	}
	mes "[寇凌]";
	mes "爸爸在哪里?";
	mes "我真的很希望爸爸可以常回家...";
	close;
}