//====================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ---------------------------------
// - 艾尔帕兰 地区任务脚本 - 
//====================================
// 版本：1.2 by Dino
//====================================
//= * 医师发箍, 羽毛帽, 话剧面具, 草帽 制作
//====================================

aldeba_in,152,166,4	script	猜谜商人#01	86,{
	mes "[猜谜商人]";
	mes "哈哈哈！ 我就是传说中那个有名的猜谜商！";
	mes "我的行迹不满整个尘世大陆！";
	mes "呵呵..其实是因为我所贩卖的商品是相当独特的东西，一边卖一边也云游四海！";
	next;
	mes "(1) . ^3355FF医师发箍^000000 : 头巾 1 个+ 铁 50 块 + 受损的钻石 + 3500 金币";
	mes "(2) . ^3355FF羽毛帽^000000 : 回忆帽 1 个 + 羽毛 300 根 + 500 金币";
	mes "(3) . ^3355FF话剧面具^000000 : 钢铁 20 块 + 哀唱草 1 株 + 5000 金币";
	mes "(4) . ^3355FF草帽^000000 : 玛哪树枝120 个 + 10000 金币";
	next;
	switch (select("医师发箍:羽毛帽:话剧面具:草帽")) {
	case 1:
		if(countitem(2275) == 0 || countitem(998) < 50 || countitem(733) == 0 || Zeny < 3500) break;
		delitem 2275,1;	//头巾
		delitem 998,50;	//铁
		delitem 733,1;	//受损的钻石
		set Zeny,Zeny - 3500;
		mes "[猜谜商人]";
		mes "嗯嗯 ！ 你 .. 有医师执照吗 ?！ 不过呢，如果你拥有像怪医黑杰克那样的医术的话，没有执照应该也无所谓啦..";
		mes "嗯，应该没什么太大的问题。那，拿去，这个给你。";
		getitem 2273,1;	//医师发箍
		close;
	case 2:
		if(countitem(2247) == 0 || countitem(916) < 300 || Zeny < 500) break;//回忆帽, 羽毛
		delitem 2247,1;//回忆帽
		delitem 916,300;//羽毛
		set Zeny,Zeny - 500;
		mes "[猜谜商人]";
		mes "嘿黑，你的眼光不赖嘛。";
		mes "虽然这些材料得来不易，但是光看这做出来的工你应该还满意吧?　那，拿去吧..";
		emotion e_heh;
		getitem 5018,1;	//羽毛帽
		close;
	case 3:
		if(countitem(999) < 20 || countitem(707) == 0 || Zeny < 5000) break;
		delitem 999,20;	//钢铁
		delitem 707,1;	//哀唱草
		set Zeny,Zeny - 5000;
		mes "[猜谜商人]";
		mes "这个应该还可以吧..";
		mes "虽然外观看起来有点令人毛骨悚然，不过..倒也蛮有它独特的风味的，你觉得如何呢?";
		mes ".. 看起来你还挺喜欢的嘛.. 那，拿走吧！";
		getitem 2281,1;	//话剧面具
		close;
	case 4:
		if(countitem(1019) < 120 || Zeny < 10000) break;
		delitem 1019,120;//玛哪树枝
		set Zeny,Zeny - 10000;
		mes "[猜谜商人]";
		mes "听说在斐扬的村落有一位制作草帽的好手..";
		mes "出自那位好手的草帽可是只有在我这里才有的喔！";
		mes "那 ！ 拿走吧 ！";
		getitem 2280,1;	//草帽
		close;
	}
	mes "[猜谜商人]";
	mes "你看你看，我不是要你再重新确认一下自己需要的物品嘛。";
	mes "年轻人你的头脑好像不太灵光喔.. 要清醒一点，知道吗?";
	close;
}