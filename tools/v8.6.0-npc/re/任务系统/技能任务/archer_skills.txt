
// ---------------------------------
// - 弓箭手任务技能 脚本 - 
//===================================
// 版本: 1.5
//===================================

morocc,121,109,5	script	爵 纳利#qsk_ac	88,{
	if (BaseClass == Job_Archer) {
		if (getskilllv(147) == 1) {
			mes "[爵 纳利]";
			mes "噢噢，是我家乡人啊!";
			mes "很高兴见到你!";
			mes "过的还不错吧?";
			mes "啊! 这箭矢!";
			mes "是自己做的啊!";
			next;
			mes "[爵 纳利]";
			mes "哈哈...!";
			mes "做的不错吗?";
			mes "哈哈... 不管怎么样，我已经很高兴了";
			mes "下一次再来找我吧，也多带点故乡消息.";
			mes "那么再见~";
			close;
		}
		else if ((Basejob == Job_Archer && JobLevel >= 30) || BaseJob == Job_Hunter || BaseJob == Job_Dancer || BaseJob == Job_Bard) {
			mes "[爵 纳利]";
			mes "啊!"; 
			mes " ";
			mes "你是...";
			next;
			if (countitem(907) > 19 && countitem(921) > 6 && countitem(7012) > 40 && countitem(1019) > 12 && countitem(501) > 0) {
				mes "[爵 纳利]";
				mes "你把东西带来了!"; 
				mes "非常谢谢你.";
				mes "那就按照约定, 我来教你技能吧.";
				next;
				delitem 907,20; //Resin
				delitem 921,7; //Mushroom_Spore
				delitem 7012,41; //Tough_Scalelike_Stem
				delitem 1019,13; //Wooden_Block
				delitem 501,1; //Red_Potion
				skill 147,1,0;
				mes "[爵 纳利]";
				mes "那么，从现在开始，就不用担心箭矢了";
				mes "对啦, 你在斐扬的时候，有没有看到";
				mes "卡浩这家伙啊?";
				mes "请你小心啊. 那家伙是";
				mes "非常残暴的家伙啊";
				next;
				mes "[爵 纳利]";
				mes "去斐扬的时候，记得小心葛豪就好";
				mes "请你记得.";
				mes "那就再见~";
				close;
			}
			else {
				mes "[爵 纳利]";
				mes "噢，在斐扬用箭的人!?";
				mes "很高兴见到你! 在这种地方";
				mes "见到同胞! 真是太高兴了!";
				mes "我独自一个人来到梦罗克";
				mes "这里很排斥外人... 呜呜呜~";
				mes "我很孤独啊~";
				next;
				if (select("很累吗. 还好吧:继续加油啊") == 1) {
					mes "[爵 纳利]";
					mes "是的. 谢谢你...";
					mes "你也在这外地";
					mes "也真的辛苦啊.";
					mes "找到箭矢非常难吧?";
					mes "所以说我都亲自，自己动手做啊.";
					next;
					select("啊，真的吗?!");
					mes "[爵 纳利]";
					mes "是的! 我把杂项物品，搜集起来之后";
					mes "可以制作箭矢.";
					mes "活在这辛苦的世界";
					mes "算是一个不错的手艺啊.";
					mes "若你想学的话，我可以教你啊.";
					next;
					select("那就拜托你了.");
					mes "[爵 纳利]";
					mes "不过可不是免费呀.";
					mes "天下没有白吃的午餐啊~";
					mes "嗯... 那就这样好了?"; 
					mes "请你帮我寻获，我所指定的物品的话.";
					mes "我就教你这技能.";
					next; 
					mes "[爵 纳利]";
					mes "我离开故乡之后，感觉到非常孤单.";
					mes "想藉着故乡的物品";
					mes "来安慰我孤单的心情啊.";
					mes "我需要在斐扬森林，橡胶树上流下来的枯木汁20个";
					mes "在斐扬商店卖的红色药水1瓶";
					mes "请你拿给我好吗."; 
					next; 
					mes "[爵 纳利]";
					mes "还需要，活在斐扬森林附近";
					mes "从树精魔物身上掉下来的，玛哪树枝13个";
					mes "森灵的鳞片梗41个";
					mes "毒魔菇的香菇芽孢7个";
					mes "你可以拿给我吗";
					next;
					mes "[爵 纳利]";
					mes "等到到时后，拿到之后我再教你技能好了";
					mes "那... 我就等你.";
					mes "等待你带来故乡的消息.";
					close;
				}
				mes "[爵 纳利]";
				if (Sex)
					mes "...嘿..小姐";
				else
					mes "...嘿..先生.";
				mes "...晚上走路小心啊";
				close;
			}
		}
		else {
			mes "[爵 纳利]";
			mes "什么... 第一次看到弓箭手吗?";
			mes "继续走你的路吧.";
			mes "若是没碰到，有水准的弓箭手的话"; 
			mes "我是不想开口的人啊!";
			close;
		}
	}
	else {
		mes "[?]";
		mes "哼... 什么?";
		mes "还有话要说吗?";
		mes "我可没有话要说啊.";
		mes "有一位贤能者，把我的对话"; 
		mes "给中断了.";
		close;
	}
}

payon,103,63,3	script	葛豪#qsk_ac	88,3,3,{
	if (BaseClass == Job_Archer) {
		if (getskilllv(148) == 1) {
			mes "[葛豪]";
			mes "又见面了."; 
			mes "还真奇怪啊.";
			mes "为什么我见到，曾经见过的人";
			mes "就会有撒娇的口语出现呢."; 
			mes "啊... 不管怎么样，很高兴又见到你.";
			mes "啊... 以后再不要来喔，啊啊...";
			close;
		}
		else if ((Basejob == Job_Archer && JobLevel >= 35) || BaseJob == Job_Hunter || BaseJob == Job_Dancer || BaseJob == Job_Bard) {
			mes "[葛豪]";
			mes "真是的... 受伤的地方很不容易好啊.";
			mes "我变成这副德行... 都是";
			mes "我太大意啦. 唉...";
			mes "虽然是这样, 可是还给我造成";
			mes "这么大的伤势...";
			next;
			mes "爵 纳利，这家伙要怎么办.";
			mes "恩嗯... 啊!";
			mes "你? 什么时候待在那里的啊?"; 
			mes "嗯... 真是高手啊.";
			mes "像你这样的高手";
			mes "当然一定会使用冲锋箭.";
			next;
			switch(select("请问那是什么啊?:现在可以教我吗?")) {
			case 1:
				mes "[葛豪]";
				mes "...你是在说笑啊?"; 
				mes "你已经到这样子的等级了";
				mes "还没有修练到冲锋箭啊?";
				mes "真是特别的人啊.";
				next;
				mes "(葛豪开始进入发呆的状态)";
				next;
				mes "[葛豪]";
				mes "好吧，就这样子吧. 我来教你";
				mes "什么叫做冲锋箭.";
				next;
				mes "[葛豪]"; 
				mes "所谓冲锋箭是指, 当攻击的同时";
				mes "把敌人往后退移的技能";
				mes "稍为花点时间瞄准，才能使用";
				mes "和魔法不一样的是"; 
				mes "施法时间不会被中断.";
				next;
				mes "[葛豪]";
				mes "在近距离会给较大的伤害力";
				mes "对弓箭手来说，是一个不错的技能."; 
				mes "若你想学的话，我可以教你啊.";
				next;
				mes "[葛豪]";
				mes "首先要改造弓";
				mes "请你寻找一个不用的十字弓给我";
				mes "还有触须10个 和鸟嘴10个";
				mes "猴子尾巴3个, 这弹力不错.";
				mes "还有绿宝石2个. 最后还需要...";
				mes "我最喜欢的香蕉汁36瓶!";
				next; 
				mes "[葛豪]"; 
				mes "......咳咳!";
				mes "这样子准备好之后，给我的话";
				mes "我会传授给你冲锋箭技能.";
				mes "那就, 请你出发吧.";
				mes "(假如没有带香蕉汁的话，我要痛扁他.)";
				close;
			case 2:
				if (countitem(721) > 1 && countitem(942) > 2 && countitem(962) > 9 && countitem(925) > 9 && countitem(532) > 35) {
					mes "[葛豪]";
					mes "不错! 很标准!";
					mes "我要把全套的弓箭技能";
					mes "冲锋箭传授给你!";
					next;
					delitem 721,2; //Azure_Jewel
					delitem 942,3; //Yoyo_Tail
					delitem 962,10; //Tentacle
					delitem 925,10; //Bill_Of_Birds
					delitem 532,36; //Banana_Juice
					skill 148,1,0;
					mes "[葛豪]";
					mes "噢, 比我想像中做的更好!";
					mes "更不需要改良弓了!"; 
					mes "这请你再带回去吧~";
					mes "从现在开始，请你尽情的享受";
					mes "在洞穴和原野上的表现了!";
					mes "哈~哈~!";
					close;
				}
				else {
					mes "[葛豪]";
					mes "嗯... 有点可惜啊.";
					mes "有个地方不足啊.";
					mes "我在重新说一边, 我需要绿宝石2个";
					mes "猴子尾巴3个, 触须10个"; 
					mes "鸟嘴10个, 最后需要"; 
					mes "香蕉汁36瓶!";
					next;
					mes "[葛豪]";
					mes "一定要找到之后，重新再来找我啊!";
					close;
				}
			}
		}
		else {
			mes "[葛豪]";
			mes "噢噢... 你是弓箭手啊."; 
			mes "稍为再努力一点的话"; 
			mes "会得到不错的名声!";
			mes "请你继续加油啊!"; 
			close;
		}
	}
	else {
		mes "[?]";
		mes "对我这人生，孤独的人来说";
		mes "还有什么事情来找我呢?";
		close;
	}

OnTouch:
	mes "[???]";
	mes "咳呜呜呜嗯....";
	next;
	mes "[???]";
	mes "咳呜呜呜嗯....";
	close;
}
