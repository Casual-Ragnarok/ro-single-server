//====================================================================
//Ragnarok Online Thief jobchange script
//====================================================================
moc_ruins.gat,141,125,1	script	怪しい奴	83,{
	if (Class == 0) goto Lnovice;
	if (Class == 6) goto Ltwice;
	if (@DOUBT_NPC == 1) goto L_SUB_1;
	if (@DOUBT_NPC == 2) goto L_SUB_2;
	if (@DOUBT_NPC == 3) goto L_SUB_3;
	if (@DOUBT_NPC == 4) goto L_SUB_4;
		mes "[怪しい奴]";
		mes "こんにちはーいい天気ですね！";
		mes "今日みたいに暖かい陽射の気持ちいい午後はピラミッド付近でピクニックするのが一番だよね。";
		set @DOUBT_NPC,@DOUBT_NPC+1;
		close;
L_SUB_1:
		mes "[怪しい奴]";
		mes "'帰る家があるということは幸せの第一歩である。";
		mes "私が一番好きなことわざです。帰る家か…";
		set @DOUBT_NPC,@DOUBT_NPC+1;
		close;
L_SUB_2:
		mes "[怪しい奴]";
		mes "............... ";
		set @DOUBT_NPC,@DOUBT_NPC+1;
		close;
L_SUB_3:
		mes "[怪しい奴]";
		mes "おー私はあ、や、し、い、人ではありません。";
		mes "そんな目で見ないでく、だ、さ、い。";
		set @DOUBT_NPC,@DOUBT_NPC+1;
		close;
L_SUB_4:
		mes "[怪しい奴]";
		mes "…しつこいな。";
		mes "もう話すこともないからあっち行けよ。";
		close;
Lnovice:
	if (change_thief == 2) goto LStart2;
	if (change_thief == 3) goto LStart3;
	mes "[怪しい奴]";
	mes "そこの初心者！素晴らしい";
	mes "人生を送りたいか？もっと楽な生活を";
	mes "したいか？ならばシーフになれ！";
	mes "そしてシーフギルドに入団するのだ！";
	next;
	mes "[怪しい奴]";
	mes "シーフギルドはいつも君達を";
	mes "歓迎する！今すぐ加入したら";
	mes "ポイントカード付き！(冗談)";
	mes "場所は、ピラミッドダンジョン1階";
	mes "中央の階段を下りて左だ！右かも";
	mes "しれない！まっすぐ行ったら駄目！";
	close;
LStart2:
	mes "[シーフギルド関係者]";
	mes "君…妙な匂いがする…";
	mes "転職の試験を受けに来たな。";
	mes "名前は？ " + strcharinfo(0) + " か…";
	mes "え…うん、名簿にあるな。よし！";
	mes "送るぞ！しかし、どこに落ちるかは";
	mes "知らないよ！イェイー！";
	close2;
	set change_thief,3;
	warp "job_thief1.gat",38,50;
	end;
LStart3:
	mes "[シーフギルド関係者]";
	mes "うはははは！なんだー君、";
	mes "また転職出来なかった？アホウかよ！";
	mes "くはははは！あっ、まあ、シーフは";
	mes "みんな馬鹿しか来ないからな！";
	mes "くはははは！";
	mes "よし！送るぞ！";
	close2;
	warp "job_thief1.gat",38,50;
	end;
Ltwice:
	mes "[シーフギルド関係者]";
	mes "へえ、立派なシーフになったな。";
	mes "でも、ここはもう入れないよ。";
	mes "君も知ってると思うけど、ここは";
	mes "転職希望の奴しか入れない";
	mes "試験場だから。もっと面白い所へ、";
	mes "美味しい物を拾いに行ってくれ。";
	close;
}
//==============================================================================
moc_prydb1.gat,42,133,2	script	ギルド関係者	118,{
	if (Class == 6) goto Ltwice;
	if ((change_thief == 2) && (Class == 0)) goto LStart2;
	if ((change_thief == 3) && (Class == 0)) goto LStart3;
	if (Class == 0) goto Lnovice;
	mes "[シーフギルド関係者]";
	mes "おーい、おーい、そこの奴！";
	mes "ここで何してる？";
	mes "ここはお前の居場所がないから、";
	mes "早く出てもらおう。";
	close;
Lnovice:
	mes "[シーフギルド関係者]";
	mes "うん？";
	mes "初心者のくせにここに何の用かな？";
	mes "もし、シーフに転職したいなら、";
	mes "俺の隣の姉ちゃんに聞けよ。";
	close;
LStart2:
	mes "[シーフギルド関係者]";
	mes "へっ、面接は無事に終わったか？";
	mes "次はキノコ盗りか。";
	mes "せいぜい頑張る事だ。";
	close;
LStart3:
	mes "[シーフギルド関係者]";
	mes "うん…?あ、戻ったな。さあ、";
	mes "キノコ見せてよ。点数つけてやるから。";
	next;
	set @kinokoA,countitem(1069)*3;
	set @kinokototal,@kinokoA+countitem(1070);
	mes "[シーフギルド関係者]";
	if (countitem(1069) > 0) mes "紅の毛網キノコ " + countitem(1069) + " 個。";
	if (countitem(1070) > 0) mes "紅の毛ベトベトキノコ " + countitem(1070) + " 個。";
	mes "評価結果…え… " + @kinokototal + " 点だ。";
	next;
	if (@kinokototal < 25) goto Llownum;
		mes "[シーフギルド関係者]";
		mes "よし、合格だ！";
		mes "これからギルドの転職儀式を行う！";
		next;
		delitem 1069,countitem(1069);
		delitem 1070,countitem(1070);
		jobchange 6;
		set change_thief,99;
		mes "[シーフギルド総帥 'E']";
		if (Sex == 1) mes "'姓名 " + strcharinfo(0) + " 殿。'";
		if (Sex == 0) mes "'姓名 " + strcharinfo(0) + " 嬢。'";
		mes "全ての試験をパスしたので、";
		mes "君をシーフとして認める。同時に";
		mes "シーフギルドへの正式入団を許可する。";
		next;
		mes "[シーフギルド総帥 'E']";
		mes "シーフ転職をお祝いする。";
		mes "以後、シーフギルドの一員として、";
		mes "ギルドの秩序と規範を守れ。万が一、";
		mes "はぐれた行動やルール違反などで、";
		mes "シーフギルドに被害があった場合、";
		next;
		mes "[シーフギルド総帥 'E']";
		mes "君の存在はギルドによって";
		mes "抹消される事をくれぐれも忘れずに。";
		mes "まあ、素晴らしい活動を期待する。";
		mes "以上。";
		next;
		mes "[ブレード]";
		mes "はははは！入団おめでとう！";
		mes "わが同士よ！俺の名は<ブレード>、";
		mes "ここシーフギルドの転職と";
		mes "会員管理担当だ。これから";
		mes "顔合わせる機会が多いと思うから、";
		mes "よろしく。";
		next;
		set @kinokototal,@kinokototal*10;
		set Zeny,Zeny+50+@kinokototal;
		mes "[ブレード]";
		mes "さあ、これ。";
		mes "新米に支給される補助金と";
		mes "キノコの代金だ。";
		next;
		mes "[ブレード]";
		mes "大体終わったから、帰ってもいい。";
		mes "俺は忙しいんだ。";
		mes "それじゃ、またな。";
		close;
Llownum:
	mes "[シーフギルド関係者]";
	mes "まだまだだっ！もっと盗って来い！";
	close;
Ltwice:
	mes "[ブレード]";
	mes "今は特にイベントがないんだ。";
	mes "連絡したら集合することだ。";
	close;
}
//==============================================================================
moc_prydb1.gat,39,129,2	script	ギルド関係者	69,{
	if (Class == 6) goto Ltwice;
	if (change_thief == 1) goto LStart2;
	if (change_thief == 2) goto LStart3;
	if (change_thief == 3) goto LStart4;
	if (Class == 0) goto Lnovice;
		mes "[シーフギルド関係者]";
		mes "お前はなんだ…？";
		next;
		if (Class >= 7) goto Lnext;
		mes "[シーフギルド関係者]";
		if (Class == 1) mes "馬鹿な剣士か！";
		if (Class == 2) mes "サーカス団の魔術師か！";
		if (Class == 3) mes "アホウのアーチャーだな！";
		if (Class == 4) mes "世界最強芸能人崇拝者アコか！";
		if (Class == 5) mes "悪質な商人か！";
		next;
	Lnext:
		mes "[シーフギルド関係者]";
		if (Sex == 1) mes "おい、兄ちゃん。";
		if (Sex == 0) mes "ほら、姉ちゃん。";
	   	mes "何しに来た？帰れ、帰れ！";
		close;
Lnovice:
	mes "[シーフギルド関係者]";
	if (Sex == 1) goto Lman;
		mes "へえ…お嬢様が何の用で";
		mes "こんな汚い所まで";
		mes "いらっしゃたかな？";
		goto Lchoose;
	Lman:
		mes "へぇ…坊ちゃまが何の用で";
		mes "こんな汚い所までわざわざ";
		mes "来たのかな？";
		goto Lchoose;
	LStart2:
		mes "[シーフギルド関係者]";
		mes "おーまた来たか。";
		mes "今度は間違いないだろう？";
Lchoose:
	next;
	menu "シーフになりたい。",L1,"なんでもない。",-;
		close;
	L1:
		if (change_thief == 1) goto L1_twice;
		mes "[シーフギルド関係者]";
		mes "…自慢する事はないだろう？";
		mes "シーフになりたいって、そんな";
		mes "恥ずかしい事がよく言えるな。";
		mes "馬鹿か？道歩いてる人に私は";
		mes "シーフになりますって、広告でもする";
		mes "気か？ちょっと、頭に何がある？";
		next;
		mes "[シーフギルド関係者]";
		mes "本当にシーフになりたいのか？";
		next;
		menu "うん。",L1_1,"ううん。",L1_2,"そんなこと言ってる君は？",L1_3;
		L1_1:
			mes "[シーフギルド関係者]";
			mes "ふん…";
			mes "まあ、そーだったら仕方ないか。";
			goto L1_f;
		L1_2:
			mes "[シーフギルド関係者]";
			mes "だったら、なんだよ？ここに";
			mes "恋人でもいるのか？じゃなかったら";
			mes "シーフギルドまで何しに来た？";
			goto L1_f;
		L1_3:
			mes "[シーフギルド関係者]";
			mes "え…？あたし…？";
			mes "まあ、あたしは…ただ…うう、";
			mes "細かい事は聞くな！";
		L1_f:
			mes "とりあえず、現実世界でシーフに";
			mes "なりたいとか言ったら駄目だよ！";
			next;
			mes "[シーフギルド関係者]";
			mes "本当に、";
			mes "本当にシーフになりたいのか？";
			next;
			menu "うん。",L1_f2,"やっぱりやめる。",-;
				mes "[シーフギルド関係者]";
				mes "なんだよ。転職もしないのに…";
				mes "本当に何しに来た？";
				mes "何を考えているんだよ…？";
				close;
			L1_f2:
				mes "[シーフギルド関係者]";
				mes "だったら君の名前を言って。";
				mes "うん？なに？";
				mes "" + strcharinfo(0) + "." + strcharinfo(0) + " って…？";
				next;
//転生ノビ処理
	callfunc "jobchanger","シーフギルド関係者",6;
				set change_thief,1;
				mes "[シーフギルド関係者]";
				mes "申請書はOK。";
				mes "転職条件をもう知ってるんだったら、";
				mes "このまま面接に入るよ。";
				mes "やってみるか？";
				next;
				menu "うん、いいよ。",Lcheck,"いや、後で。",-;
					mes "[シーフギルド関係者]";
					mes "まあ、好きにして。ばいばい〜。";
					close;
L1_twice:
	mes "[シーフギルド関係者]";
	mes "さあ、それでは、確認しようか？";
	next;
Lcheck:
	mes "[シーフギルド関係者]";
	mes "名前が… " + strcharinfo(0) + "。えっと…";
	mes "あ、これだな。姓名 " + strcharinfo(0) + "。";
	mes "ちょっと〜待って〜。";
	next;
	if (getskilllv(1) < 9) goto Llowlv;
	mes "[シーフギルド関係者]";
	mes "うん…資料を見たらある程度興味";
	mes "深い部分もあるし…基本能力も";
	mes "出来てるし…いいよ。";
	mes "姓名 " + strcharinfo(0) + "。これで、";
	mes "シーフギルドの面接は終わり！";
	next;
	mes "[シーフギルド関係者]";
	mes "さあ、今度はシーフとしての";
	mes "才能があるかどうかだ。";
	mes "試験については知ってるか？";
	next;
	menu "もちろん知ってる。",-,"そんなこと知ってるわけないだろう。",Lcheck2;
		mes "[シーフギルド関係者]";
		mes "そう？よかったな。";
		goto Lcheckend;
Lcheck2:
		mes "[シーフギルド関係者]";
		mes "知らない？仕方ないな。";
		mes "よく聞けよ。";
		next;
		mes "[シーフギルド関係者]";
		mes "モロクで、一番悪質で有名な商人";
		mes "<シブ>が営んでいるキノコ農場に";
		mes "行って、そこでキノコを盗んで来るのが";
		mes "転職試験だ！盗るキノコは^0000ff紅の毛網キノ";
		mes "コ^000000と";
		mes " ^0000ff紅の毛ベトベトキノコ^000000 の２種類";
		next;
		mes "[シーフギルド関係者]";
		mes "キノコを盗ってまたここに戻ったら、";
		mes "採点をする。";
		mes "<紅の毛網キノコ>は１つで３点、";
		mes "<紅の毛ベトベトキノコ>は１つで";
		mes "１点。全部あわせて２５点以上";
		mes "だったら合格だ。";
		next;
		mes "[シーフギルド関係者]";
		mes "行き方は…ピラミッドを出てから";
		mes "町の方に行った先の、２つの";
		mes "柱の間に隠されているうちの仲間に";
		mes "頼んだらいい。奴に話し掛けたら、";
		mes "キノコの裏道に送ってくれるから。";
		next;
		mes "[シーフギルド関係者]";
		mes "正確な位置は多分…^ff0000141. 125^000000'";
		mes "だったかな？";
		mes "お前の現在位置が知りたいなら、";
		mes "</where>というコマンドを";
		mes "書くと、位置が表示されるから、";
		mes "参考にしなよ。";
Lcheckend:
		next;
		mes "[シーフギルド関係者]";
		mes "行く前に忠告したいのは、";
		next;
		mes "[シーフギルド関係者]";
		mes "キノコ農場にはモンスターがいる";
		mes "ことだ。…知ってるか？遊び気分で";
		mes "行ったら、痛い目に遭うから、";
		mes "気を引き締めて行った方がいい。";
		next;
		mes "[シーフギルド関係者]";
		mes "君の判断でモンスターを倒したり、";
		mes "逃げたりして、適当にやって行く事だ。";
		next;
		mes "[シーフギルド関係者]";
		mes "次はキノコだけど、転職試験といえ、";
		mes "君がこれからする事は犯罪だから。";
		mes "転職のためには悪になるしか";
		mes "ないって事だ。それが出来なかったら";
		mes "シーフの資格はない。そーだろう？";
		mes "ほら、はっきりケジメをつけろ。";
		next;
		mes "[シーフギルド関係者]";
		mes "この以上あたしが言う事はない。";
		mes "そろそろ出発して！";
		set change_thief,2;
		close;
LStart3:
	mes "[シーフギルド関係者]";
	mes "ここで何してるんだ？";
	mes "キノコ盗りに行かないのか？";
	mes "もしかして、転職条件を忘れた？";
	mes "もう一度教えようか？";
	next;
	menu "うん。",LS3_1,"ううん。",LS3_2;
	LS3_1:
		mes "[シーフギルド関係者]";
		mes "まったく、しっかり覚えとけよ。";
		mes "いつもお前みたいな奴が1人は";
		mes "出るからな。";
		next;
		mes "[シーフギルド関係者]";
		mes "モロクで、一番悪質で有名な商人";
		mes "<シブ>が営んでいるキノコ農場に";
		mes "行って、そこでキノコを盗んで来るのが";
		mes "転職試験だ！盗るキノコは^0000ff紅の毛網キノ";
		mes "コ^000000と";
		mes " ^0000ff紅の毛ベトベトキノコ^000000 の２種類";
		next;
		mes "[シーフギルド関係者]";
		mes "キノコ農場にはキノコだけでなく、";
		mes "モンスターもいるから";
		mes "気をつけることだ。";
		next;
		mes "[シーフギルド関係者]";
		mes "キノコを盗ってまたここに戻ったら、";
		mes "採点をする。";
		mes "<紅の毛網キノコ>は１つで３点、";
		mes "<紅の毛ベトベトキノコ>は１つで";
		mes "１点。全部あわせて２５点以上";
		mes "だったら合格だ。";
		next;
		mes "[シーフギルド関係者]";
		mes "行き方は…ピラミッドを出てから";
		mes "町の方に行った先の、２つの";
		mes "柱の間に隠されているうちの仲間に";
		mes "頼んだらいい。奴に話し掛けたら、";
		mes "キノコの裏道に送ってくれるから。";
		next;
		mes "[シーフギルド関係者]";
		mes "正確な位置は多分…^ff0000141. 125^000000'";
		mes "だったかな？";
		mes "お前の現在位置が知りたいなら、";
		mes "</where>というコマンドを";
		mes "書くと、位置が表示されるから、";
		mes "参考にしなよ。";
		close;
	LS3_2:
		mes "[シーフギルド関係者]";
		mes "…なんだよ？";
		mes "特に用もないのに話し掛けて";
		mes "…喧嘩売ってるのかよ？うん？";
		close;
LStart4:
	mes "[シーフギルド関係者]";
	mes "お？言ってみただけなのに、";
	mes "本当にキノコを取ってきた？";
	mes "お前、アホウだろう？";
	mes "騙されたんだよ。わかる？";
	next;
	mes "[シーフギルド関係者]";
	mes "なーんちゃって！ウソだよー。";
	mes "ここはシーフになろうとする";
	mes "バカモノ達がくる所だ。";
	mes "転職したいなら、隣の奴に聞いてくれ。";
	close;
Llowlv:
	mes "[シーフギルド関係者]";
	mes "シーフになりたいという";
	mes "君の意志はわかるが…";
	mes "意志だけで世の中生きて";
	mes "行けないんだよ！最低でも";
	mes "初心者スキル９にならないと駄目だ！";
	close;
Ltwice:
	mes "[シーフギルド関係者]";
	mes "よー。";
	mes "悩みがあったらいつでも";
	mes "このおねーさんに相談してよ！";
	mes "うん？";
	close;
}
