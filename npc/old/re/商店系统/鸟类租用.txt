//========================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// -----------------------------------------
// 大嘴鸟、大嘴鸟、猎鹰租用脚本
//=========================

// 大嘴鸟 管理兵 (for Knights)-------------------------------------------
prontera,55,350,5	script	大嘴鸟 管理兵#knt	105,{
	if(Upper==0) set .@price,2500; //普通大嘴鸟价格
	if(Upper==1) set .@price,2500; //转生
	if(Upper==2) set .@price,2500; //养子

	mes "[大嘴鸟 管理兵]";
	if (BaseJob == Job_Knight && Upper < 4) {
		mes "欢迎光临，你要租大嘴鸟吗?";
		mes "租用一只大嘴鸟需要 "+.@price+" Zeny, 同时, 你必须学过骑乘术才能驾驭它.";
		next;
		if (select("租用:好贵啊") == 1) {
			if (zeny < .@price) {
				mes "[大嘴鸟 管理兵]";
				mes "您的金钱不足.";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[大嘴鸟 管理兵]";
				mes "你没学过骑乘术呢";
				mes "要学过骑乘术才能使用大嘴鸟喔";
				close;
			}
			else if(checkriding()) {
				mes "[大嘴鸟 管理兵]";
				mes "往你的屁股下面看看";
				mes "你已经骑在大嘴鸟上面了......";
				close;
			}
			set zeny,zeny-.@price;
			setriding;
		}
		mes "[大嘴鸟 管理兵]";
		mes "那么, 这只大嘴鸟就交给你了";
		mes "要好好照顾他喔！";
		close;
	}
	mes "请问有什么事吗?";
	mes "我们的大嘴鸟是^4444FF 骑士 ^000000专用的";
	close;
}

// 大嘴鸟 管理兵 (for Crusaders)-------------------------------------------
prontera,232,318,3	script	大嘴鸟 管理兵#cru	105,{
	if(Upper==0) set .@price,3500; //Normal Peco - default price
	if(Upper==1) set .@price,3500; //Armored Peco
	if(Upper==2) set .@price,3500; //Baby Peco

	mes "[大嘴鸟 管理兵]";
	if (BaseJob == Job_Crusader && Upper < 4) {
		if(Upper != 1 ) mes "Welcome, Crusader.";
		else mes "Welcome, Paladin.";
		mes "欢迎光临，你要租大嘴鸟吗?";
		mes "租用一只大嘴鸟需要 "+.@price+" Zeny, 同时, 你必须学过骑乘术才能驾驭它.";
		next;
		if (select("租用:好贵啊") ==1) {
			if (zeny < .@price) {
				mes "[大嘴鸟 管理兵]";
				mes "您的金钱不足.";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[大嘴鸟 管理兵]";
				mes "你没学过骑乘术呢";
				mes "要学过骑乘术才能使用大嘴鸟喔";
				close;
			}
			else if(checkriding()) {
				mes "[大嘴鸟 管理兵]";
				mes "往你的屁股下面看看";
				mes "你已经骑在大嘴鸟上面了......";
				close;
			}
			set zeny,zeny-.@price;
			setriding;
		}
		mes "[大嘴鸟 管理兵]";
		mes "那么, 这只大嘴鸟就交给你了";
		mes "要好好照顾他喔！";
		close;
	}
	mes "请问有什么事吗?";
	mes "我们的大嘴鸟是^4444FF 十字军 ^000000专用的";
	close;
}

// Falcon Master--------------------------------------------------------------
hu_in01,381,304,5	script	猎鹰 管理员	105,{
	if(Upper==0) set .@price,2500; //Normal Falcon - default price
	if(Upper==1) set .@price,2500; //Scarf Falcon
	if(Upper==2) set .@price,2500; //Baby Falcon
	if(Upper&4) set .@price,2500; //游侠猎鹰

	mes "[猎鹰 管理员]";
	if (BaseJob == Job_Hunter) {
		mes "哈! 我是猎鹰 管理员.";
		mes "你可以花费 "+.@price+" z 租用猎鹰，不过你要先学会 ^5555FF'驯鹰术'^000000 ";
		next;
		if (select("我要租猎鹰:不用了，谢谢") == 1) {
			if (zeny < .@price) {
				mes "[猎鹰 管理员]";
				mes "您的金钱不足.";
				close;
			}
			else if(getskilllv("HT_FALCON") == 0) {
				mes "[猎鹰 管理员]";
				mes "对不起，你必须至少学会一级的训鹰术技能才能使用猎鹰。";
				close;
			}
			else if(checkfalcon()) {
				mes "[猎鹰 管理员]";
				mes "往你的头上看看";
				mes "你的猎鹰正在你头上盘旋......";
				close;
			}
			set zeny,zeny-.@price;
			setfalcon;
			close;
		}
		mes "[猎鹰 管理员]";
		mes "祝你有个美好的一天";
		close;
	}
	mes "抱歉，猎鹰只能租给猎人.";
	close;
}

prontera,163,178,3	script	魔导机械管理兵#Mechanic	105,{
	if(baseClass != Job_Merchant || !(Upper&4)){
		mes "[魔导机械管理兵]";
		mes "你有什么事情吗?";
		mes "魔导机械是机械师专用的东西.";
		close;
	}
	if(!getskilllv("NC_MADOLICENCE")) {
		mes "[魔导机械管理兵]";
		mes "请先获得魔导机械执照.";
		close;
	}
	mes "[魔导机械管理兵]";
	mes "是机械师啊. 你想使用手推车呢?";
	mes "还是想搭乘魔导机械呢?";
	next;
	switch(select("租用手推车:搭乘魔导机械:不搭乘")) {
	case 1:
		if(checkcart()) {
			mes "[魔导机械管理兵]";
			mes "你现在正在使用一辆手推车.";
			close;
		}
		if(checkriding()) {
			mes "[魔导机械管理兵]";
			mes "租用手推车会收回你的魔导机械.";
			mes "确认租用吗?";
			next;
			if(select("是的:不") == 2) close;
		}
		mes "[魔导机械管理兵]";
		mes "希望你好好使用~";
		setriding 0;
		setcart;
		break;
	case 2:
		if(checkriding()) {
			mes "[魔导机械管理兵]";
			mes "你已经搭乘魔导机械了.";
			close;
		}
		if(checkcart()) {
			mes "[魔导机械管理兵]";
			mes "租用手推车会收回你的魔导机械.";
			mes "确认租用吗?";
			next;
			if(select("是的:不") == 2) close;
		}
		mes "[魔导机械管理兵]";
		mes "请坐上去, 希望你好好使用~";
		setcart 0;
		setriding;
		break;
	}
	close;
}

prontera,130,213,5	script	兽骑管理兵#Royal	105,{
	if(baseClass != Job_Swordman || !(Upper&4)){
		mes "[兽骑管理兵]";
		mes "这里是为了符文骑士, 皇家卫士";
		mes "管理兽骑的地方.";
		close;
	}
	if(BaseJob == Job_Knight && !getskilllv("RK_DRAGONTRAINING")) {
		mes "[兽骑管理兵]";
		mes "请你先学会训龙术.";
		close;
	}
	if(BaseJob == Job_Crusader && !getskilllv("KN_RIDING")) {
		mes "[兽骑管理兵]";
		mes "请你先学会骑乘术.";
		close;
	}
	mes "[兽骑管理兵]";
	if(BaseJob == Job_Knight) mes "欢迎光临, 想领一只龙吗?";
	if(BaseJob == Job_Crusader) mes "欢迎光临, 想领一只狮鹫吗?";
	next;
	if(select("是的:不") == 2) {
		mes "[兽骑管理兵]";
		mes "好吧, 辛苦你了.";
		close;
	}
	if(checkriding()) {
		mes "[兽骑管理兵]";
		if(BaseJob == Job_Knight) mes "你已经骑了一只龙了.";
		if(BaseJob == Job_Crusader) mes "你已经骑了一只狮鹫了";
		close;
	}
	mes "[兽骑管理兵]";
	mes "请你坐好~";
	setriding;
	close;
}

prontera,125,208,5	script	兽骑消除兵#Royal	105,{
	mes "[兽骑消除兵]";
	mes "这项服务是专为骑乘兽骑, 但因";
	mes "为某些特殊情况, 必须从兽骑上";
	mes "下来的人士准备的.";
	if(baseClass != Job_Swordman || !checkriding()) close;
	next;
	mes "[兽骑消除兵]";
	if(BaseJob == Job_Knight && Upper < 4) mes "你骑乘的是大嘴鸟呀.";
	if(BaseJob == Job_Knight && Upper&4) mes "你骑乘的是龙呀.";
	if(BaseJob == Job_Crusader && Upper < 4) mes "你骑乘的是钢甲大嘴鸟呀.";
	if(BaseJob == Job_Crusader && Upper&4) mes "你骑乘的是狮鹫呀.";
	mes "你想要下来吗?";
	next;
	if(select("是的:不") == 2) {
		mes "[兽骑消除兵]";
		mes "好吧, 改主意了就回来找我.";
		close;
	}
	mes "[兽骑消除兵]";
	mes "很久没用脚走路了吧.";
	mes "感觉怎么样?";
	setriding 0;
	close;
}